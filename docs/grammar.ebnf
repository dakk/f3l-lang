<program> ::= <declaration>* EOF

<parameter> ::= IDENT COLON <type_sig>

<param_opt_typed> ::= IDENT
                    | IDENT COLON IDENT

<ident> ::= IDENT

<type_sig> ::= TANY
             | <ident>
             | <type_expr> CONT
             | LPAR <type_sig> MUL <type_sig> RPAR
             | LBRACE <parameter> (SEMICOLON <parameter>)* RBRACE
             | <ident> PIPE <ident> (PIPE <ident>)*
             | LPAR <type_sig> RPAR
             | <type_sig> LAMBDA <type_sig>

<type_expr> ::= <type_sig>

<erec_element> ::= IDENT EQ <expr>

<left> ::= IDENT (DOT IDENT)*

<expr> ::= UNIT
         | TRUE
         | FALSE
         | STRING
         | BYTES
         | FLOAT
         | INT
         | NAT
         | NONE
         | SOME LPAR <expr> RPAR
         | LBRACE <erec_element> (SEMICOLON <erec_element>)* RBRACE
         | LSQUARE [<expr> (COMMA <expr>)*] RSQUARE
         | LPAR <expr> COMMA <expr> RPAR
         | FUN LPAR [<parameter> (COMMA <parameter>)*] RPAR LAMBDA <expr>
         | FUN <param_opt_typed> LAMBDA <expr>
         | LET IDENT COLON <type_sig> EQ <expr> IN <expr>
         | LET IDENT EQ <expr> IN <expr>
         | LET REC IDENT COLON <type_sig> EQ <expr> IN <expr>
         | LET REC IDENT EQ <expr> IN <expr>
         | <expr> ADD <expr>
         | <expr> SUB <expr>
         | <expr> DIV <expr>
         | <expr> MUL <expr>
         | <expr> MOD <expr>
         | <expr> AND <expr>
         | <expr> OR <expr>
         | NOT <expr>
         | <expr> LT <expr>
         | <expr> LTE <expr>
         | <expr> GT <expr>
         | <expr> GTE <expr>
         | <expr> EQ <expr>
         | <expr> NEQ <expr>
         | IF <expr> THEN <expr> ELSE <expr>
         | IDENT
         | <left> DOT IDENT
         | <expr> DOT IDENT
         | <left> LPAR <expr> RPAR
         | <expr> LPAR <expr> RPAR
         | LPAR <expr> RPAR
         | LPAR <expr> COLON <type_sig> RPAR

<dtype> ::= TYPE TANY <ident> EQ <type_sig>
          | TYPE IDENT EQ <type_sig>

<ddef> ::= LET IDENT COLON <type_expr> EQ <expr>
         | LET IDENT EQ <expr>
         | LET REC IDENT COLON <type_expr> EQ <expr>
         | LET REC IDENT EQ <expr>

<dexternal> ::= EXTERNAL IDENT COLON <type_expr> EQ STRING

<dopen> ::= OPEN <ident>

<declaration> ::= <dtype>
                | <ddef>
                | <dexternal>
                | <dopen>


